// Horizontal whitespace only (no newlines)
ws = _{ " " | "\t" }

program = {
    SOI ~ (line ~ NEWLINE)* ~ line? ~ EOI
}

line = {
    ws* ~ (instruction_line | label_only_line | comment_line | empty_line)
}

// Label on its own line (no instruction)
label_only_line = {
    label ~ ws* ~ (";" ~ ws* ~ comment)?
}

// Instruction with optional preceding label
instruction_line = {
    (label ~ ws*)? ~ instruction ~ ws* ~ (";" ~ ws* ~ comment)?
}

label = {
    identifier ~ ws* ~ ":"
}

comment_line = {
    ";" ~ ws* ~ comment
}

empty_line = {
    ws*
}

instruction = {
    opcode ~ ws+ ~ operands
}

opcode = {
    ASCII_ALPHA+
}

operands = {
    ((register | literal | identifier) ~ ws* ~ ","? ~ ws*)+
}

comment = {
    (!NEWLINE ~ ANY)*
}

register = {
    ("R" | "r") ~ ASCII_DIGIT
}

literal = {
    "#"? ~ "-"? ~ ASCII_DIGIT+
}

identifier = {
    !register ~ ASCII_ALPHA ~ (ASCII_ALPHA | ASCII_DIGIT | "_")*
}